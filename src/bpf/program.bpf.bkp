 /*
 sudo apt install netsniff-ng, to get program bpfc
 bpfc -i filter -f C -p -D TCP (or UDP)
 pass ipv4 and tcp
 suport to interface ethernet and
 interface tun (that not has header ethernet)
*/

#define P_TCP 0x06
#define P_UDP 0x11

#if UDP
#define PROTO P_UDP
#elif TCP
#define PROTO P_TCP
#else
#error "protocol missig, use option -D UDP or TCP"
#endif

/* test in interface tun */
l0:   ldb      [0]                      /* load offset */
l1:   and      #0xf0                    /* bitwise and */
l2:   jeq      #0x40,       l3,	l11     /* if ipv4 true */
l3:   ld       [12]                     /* test source ip */
l4:   and      #0xff000000
l5:   jeq      #0x7f000000, drop, l6    /* drop src network 127.0.0.0/8 */
l6:   ld       [16]                     /* test dest ip */
l7:   and      #0xff000000
l8:   jeq      #0x7f000000, drop, l9    /* drop dst network 127.0.0.0/8 */
l9:   ldb      [9]                      /* load offset l4*/
l10:  jeq      #PROTO,   pass, l11      /* if protocol true */
/* test in interface ethernet */
l11:  ldh      [12]                     /* load half-word offset 12 */
l12:  jeq      #0x0800,     l13, drop   /* if ipv4 */
l13:  ld       [26]                     /* test source ip */
l14:  and      #0xff000000
l15:  jeq      #0x7f000000, drop, l16   /* drop network 127.0.0.0/8 */
l16:  ld       [30]                     /* test dest ip */
l17:  and      #0xff000000
l18:  jeq      #0x7f000000, drop, l19   /* drop network 127.0.0.0/8 */
l19:  ldb      [23]
l20:  jeq      #PROTO,    pass, drop    /* if protocol true */
pass: ret      #262144
drop: ret      #0
